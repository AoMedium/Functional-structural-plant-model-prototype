const ShaderRef leafmat = new ShaderRef("Leafmat");
const ShaderRef petalmat = new ShaderRef("Petalmat");
const ShaderRef internodemat = new ShaderRef("Internodemat");
const ShaderRef nodemat = new ShaderRef("Nodemat");

// buds are red spheres
module Bud(int rank, int phyllo, int order) extends Sphere(0.1) {
{ setShader(nodemat); }
}
module Node extends Sphere(0.07) {
{ setShader(nodemat); }
}
module Internode extends Cylinder(1, 0.05) {
{ setShader(internodemat); }
}
module Leaf(float al) extends Parallelogram(2, 1) {
{ setShader(leafmat); }
}
module Flower ==>
RU(180) Cone(0.3, 0.3).(setShader(internodemat))
M(-0.25) RL(90)
[ 
for (int i = 1; i <= 5;i++) (
[ RU(i*360/5) RL(20) Parallelogram(2, 1).(setShader(petalmat)) ]
)
]
RU(45)
[
for (int i = 1; i <= 5; i++) (
[ RU(i*360/5) RL(40) F(0.3, 0.1, 14)
RV(-0.3) F(0.3, 0.1, 14) RV(-0.3) F(0.3, 0.1, 14) ]
)
]
RU(-45)
[
for (int i = 1; i <= 5; i++) (
[ RU(i*360/5) RL(70) Frustum(0.7, 0.2, 0.05).(setColor(0x8DAF58)) ]
)
]
;